#!/bin/zsh
# file name: pkg-script

usage="Usage: pkg-script <mode> [options] <exp>
pkg-script has 3 modes: view, ls, grep

* view mode is to view the info and content of package matching pattern <exp>
    view mode use view(part of vim) for viewing the info so you don't have
    write permittion to the files. You can use gf to go to the file in list.
    See man grep for more info about options and expressions.
* ls mode is to list the package name with pattern <exp>
* grep mode is used to grep pkg content with pattern <exp>

They all use grep to filter the names. The [options] and <exp> are directely
passed to grep. \`man grep\` to see more info."

append_front() {
	# use ',' to separate the expressions, hope it doesn't be in the path name :)
	# http://www.kuqin.com/article/24shell/526030.html
	echo $1 | sed -e "s,^,$2,"
}

grep_name() {
	$ls -1 $1 | grep --color=auto --no-filename $@[2,-1] -
}

check_dir_exit() {
	[[ -d $1 && -x $1 ]] || {echo $1 'not accessible'; exit 255}
}

view_mode() {
	# grep package file names
	pkg_names=$(grep_name $pkg_path $*)
	# check early, exit early
	[[ $pkg_names == '' ]] && {
		[[ $color == 'always' ]] && {echo '\e[1;31m''no package found''\e[0m'}
		exit 1
	}
	file_names=($(append_front $pkg_names $pkg_path))

	# grep script file names
	spt_names=$(grep_name $spt_path $*)
	file_names=($file_names $(append_front $spt_names $spt_path))

	cd /
	view -c 'set path=/' -p ${file_names}
}

grep_mode() {
	cd ${pkg_path} 2>/dev/null
	grep --color=$color --with-filename $@ *
}

ls_mode() {
	pkgs=$($ls -hcogG1 --time-style=long-iso --sort=time ${pkg_path} | \
	sed -e '1d' -e 's/ \{1,\}/ /g' | cut -f4- -d' ' | \
	grep --color=$color --no-filename $@ -)

	if [[ x"$pkgs" != x'' ]]; then
		[[ $color == 'always' ]] && {echo -e '\e[0;32;1mmtime:           package name:\e[0m'}
		echo $pkgs
		exit 0
	else
		[[ $color == 'always' ]] && {echo '\e[1;31m''no package found''\e[0m'}
		exit 1
	fi
}

# begin execution
[[ $ARGC -le 1 || $1 == '--help' ]] && {echo $usage; exit 2}

pkg_path='/var/log/packages/'
spt_path='/var/log/scripts/'
ls='/bin/ls'

if [[ -t 1 ]]; then
	color='always'
else
	color='never'
fi

# sanity check
check_dir_exit $pkg_path
check_dir_exit $spt_path

case $1 in
	'ls' )
	shift
	ls_mode $@
	;;
	'grep' )
	shift
	grep_mode $@
	;;
	'view' )
	shift
	view_mode $@
	;;
esac
